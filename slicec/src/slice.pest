// Copyright (c) ZeroC, Inc. All rights reserved.

main = { SOI ~ file_metadata* ~ module_def* ~ EOI }

definition = { module_def | struct_def | interface_def | enum_def }

module_start = { module_kw ~ identifier }
module_def = { prelude ~ module_start ~ "{" ~ definition* ~ "}" }

struct_start = { struct_kw ~ identifier }
struct_def = { prelude ~ struct_start ~ "{" ~ data_member* ~ "}" }

interface_start = { interface_kw ~ identifier }
interface_def = { prelude ~ interface_start ~ "{" ~ operation* ~ "}" }

enum_start = { unchecked_modifier ~ enum_kw ~ identifier ~ ( ":" ~ typename )? }
enum_def = { prelude ~ enum_start ~ "{" ~ enumerator_list? ~ "}" }

return_type = { void_kw | typename | return_tuple }
return_tuple = { "(" ~ parameter_list ~ ")" }
operation = { prelude ~ return_type ~ identifier ~ "(" ~ parameter_list? ~ ")" ~ ";" }

data_member = { prelude ~ typename ~ identifier ~ ";" }

parameter_list = { ( parameter ~ "," ~ parameter_list) | ( parameter ~ ","? ) }
parameter = { prelude ~ typename ~ identifier }

enumerator_list = { ( enumerator ~ "," ~ enumerator_list ) | ( enumerator ~ ","? ) }
enumerator = { prelude ~ identifier ~ ( "=" ~ integer )? }

identifier = @{ ASCII_ALPHA ~ ( "_" | ASCII_ALPHANUMERIC )* }
global_identifier = @{ ( "::" ~ identifier )+ }
scoped_identifier = @{ identifier ~ ( "::" ~ identifier )* }

typename = {
    primitive         |
    sequence          |
    dictionary        |
    global_identifier |
    scoped_identifier
}

sequence = { sequence_kw ~ "<" ~ typename ~ ">" }
dictionary = { dictionary_kw ~ "<" ~ typename ~ "," ~ typename ~ ">" }

primitive = {
    bool_kw     |
    byte_kw     |
    short_kw    |
    ushort_kw   |
    int_kw      |
    uint_kw     |
    varint_kw   |
    varuint_kw  |
    long_kw     |
    ulong_kw    |
    varlong_kw  |
    varulong_kw |
    float_kw    |
    double_kw   |
    string_kw
}

prelude = { local_metadata* ~ doc_comment ~ local_metadata* }

file_metadata = { "[[" ~ metadata ~ "]]" }
local_metadata = { "[" ~ metadata ~ "]" }
metadata = { metadata_directive ~ ( "(" ~ metadata_arguments? ~ ")" )? }
metadata_directive = { metadata_identifier ~ (":" ~ metadata_identifier)? }
metadata_identifier = { (ASCII_ALPHANUMERIC | "_" | "-")+ }
metadata_argument = {
    "\"" ~ (!"\"" ~ ANY)* ~ "\"" |          // Add support for escaped characters in the future.
    (ASCII_ALPHANUMERIC | "_" | "-" | ":" | "<" | ">")+
}
metadata_arguments = { ( metadata_argument ~ "," ~ metadata_arguments) | ( metadata_argument ~ ","? ) }

// "WHITESPACE" and "COMMENT" are special rules that Pest will implicitely allow to appear in between any other rules.
WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ line_comment | block_comment }
doc_comment = { ( line_doc_comment+ | block_doc_comment )? }

line_comment = _{ "//" ~ ( !"\n" ~ ANY )* }
line_doc_comment = { "///" ~ ( !"\n" ~ ANY )* }
block_comment = _{ "/*" ~ ( !"*/" ~ ANY )* ~ "*/" }
block_doc_comment = { "/**" ~ ( !"*/" ~ ANY )* ~ "*/" }

integer = { "-"? ~ ASCII_DIGIT+ }

unchecked_modifier = { unchecked_kw? }

module_kw = { "module" }
struct_kw = { "struct" }
interface_kw = { "interface" }
enum_kw = { "enum" }

sequence_kw = { "sequence" }
dictionary_kw = { "dictionary" }

void_kw = { "void" }
bool_kw = { "bool" }
byte_kw = { "byte" }
short_kw = { "short" }
ushort_kw = { "ushort" }
int_kw = { "int" }
uint_kw = { "uint" }
varint_kw = { "varint" }
varuint_kw = { "varuint" }
long_kw = { "long" }
ulong_kw = { "ulong" }
varlong_kw = { "varlong" }
varulong_kw = { "varulong" }
float_kw = { "float" }
double_kw = { "double" }
string_kw = { "string" }

unchecked_kw = { "unchecked" }
